/* -GRAMMAR---------------------------------------------------------------------
Phorward Foundation Toolkit
Copyright (C) 2006-2016 by Phorward Software Technologies, Jan Max Meyer
http://www.phorward-software.com ++ contact<at>phorward<dash>software<dot>com
All rights reserved. See LICENSE for more information.

File:	bnf.syn
Usage:	Grammar for grammar definition language ;)
----------------------------------------------------------------------------- */

// Whitespace ------------------------------------------------------------------

%skip			/[ \t\r\n]+/ ;
%skip			/\/\/[^\n]*\n/ ;
%skip			/\/\*([^*]|\*[^\/])*\*\// ;

// Terminals -------------------------------------------------------------------

@ccl 			/\[(\\.|[^\\\]])*\]/ ;
@string 		/'[^']*'/ ;
@token			/"[^"]*"/ ;
@regex 			/\/(\\.|[^\\\/])*\// ;

@ident			/[A-Za-z_][A-Za-z0-9_]*/ ;
@int			/[0-9]+/ ;
@function		/[A-Za-z_][A-Za-z0-9_]*\(\)/ ;

@flag_emit		'@' ;
@flag_goal		'$' ;
@flag_ignore	/%(ignore|skip)/ ;

// Nonterminals ----------------------------------------------------------------

@inline			: flag_emit ident? '(' alternation ')'
				|  '(' alternation ')'
				;

@symbol 		: ident | ccl | string | token | regex | function | inline ;

modifier		: @kle( symbol '*' )
				| @pos( symbol '+' )
				| @opt( symbol '?' )
				| symbol
				;

sequence		: sequence modifier | modifier ;

production	 	: sequence | ;

@alternative	: production flags? ;

alternation		: alternation '|' alternative | alternative ;

@nontermdef		: flag_emit? ident flag_goal? ( ':' alternation )? ';' ;

@termdef		: ( flag_emit? ident | flag_ignore )
					( ccl | string | regex | function ) ';'
				;

definition		: nontermdef | termdef ;

grammar	$		: definition+ ;
