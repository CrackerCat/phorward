ident		= /[A-Za-z_][A-Za-z0-9_]*/ ;
ccl 		= /'.*'/ ;
string 		= /".*"/ ;
regex 		= /\/(\\.|[^\\\/])*\// ;

symbol 		: ident | ccl | string | regex ;

mod_kleene	: symbol '*';
mod_positive: symbol '+' ;
mod_optional: symbol '?';
modifier	: mod_kleene | mod_positive | mod_optional | symbol ;

sequence	: sequence modifier | modifier ;
alternative : sequence | ;
alternation	: alternation '|' alternative | alternative ;

nontermdef	: ident ':' alternation ';' ;
termdef		: ident '=' ccl ';'
			| ident '=' string ';'
			| ident '=' regex ';'
			;

definition	: nontermdef | termdef ;
definitions	: definitions definition | definition ;
grammar		: definitions ;
